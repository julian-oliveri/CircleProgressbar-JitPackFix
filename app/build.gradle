apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'AndroidManifest.xml'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    testImplementation 'junit:junit:4.13.2'
}

def siteUrl = 'https://github.com/onaio/CircleProgressbar'
def gitUrl = 'https://github.com/onaio/CircleProgressbar.git'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.smartregister'
            artifactId = 'circleprogressbar'
            version = '1.0.16-SNAPSHOT'

            afterEvaluate {
                from components.release
            }

            pom {
                packaging = 'aar'
                name = 'org.smartregister.circleprogressbar'
                description = 'Circle ProgressBar'
                url = siteUrl

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'emre1512'
                        name = 'M. Emre Davarci'
                        email = 'emre151291@gmail.com'
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }

    repositories {
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/snapshots/")

            // Use credentials for authentication
            credentials {
                username = project.findProperty("sonatypeUsername") ?: ""
                password = project.findProperty("sonatypePassword") ?: ""
            }
        }
    }
}

